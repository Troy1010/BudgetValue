plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-parcelize'
    id 'kotlin-kapt'
    id 'TMPlugin'
    id 'dagger.hilt.android.plugin'
    id 'BuvaPlugin'
}

android {
    compileSdk 33

    compileOptions.sourceCompatibility JavaVersion.VERSION_17
    compileOptions.targetCompatibility JavaVersion.VERSION_17
    kotlinOptions.jvmTarget = JavaVersion.VERSION_17.toString()

    defaultConfig {
        applicationId "com.tminus1010.buva"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "com.tminus1010.buva.core_testing.CustomTestRunner"

        multiDexEnabled true

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation": "$projectDir/schemas".toString()]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }

    testOptions {
        unitTests.all {
            useJUnitPlatform()
        }
    }

    lint {
        error 'VisibleForTests'
    }

    namespace 'com.tminus1010.buva'
    packagingOptions {
        resources {
            excludes += ['META-INF/DEPENDENCIES', 'META-INF/LICENSE', 'META-INF/LICENSE-notice.md', 'META-INF/LICENSE.txt', 'META-INF/LICENSE.md', 'META-INF/license.txt', 'META-INF/NOTICE', 'META-INF/NOTICE.txt', 'META-INF/notice.txt', 'META-INF/ASL2.0', 'META-INF/*.kotlin_module']
        }
    }

}

budgetValuePluginSettings {
    adbAbsolutePath = android.getAdbExecutable().absolutePath
}

afterEvaluate {
    tasks.named("clean_uninstallAll").configure {
        group = "other2"
    }
}

def tmcommonkotlin_version = "1.7.1"

// Jitpack does not play nicely with libraries that depend on each other in the same project. This is a workaround.
// It might no longer be necessary.. and it might actually be fixable on the publishing side.
configurations.all {
    resolutionStrategy.dependencySubstitution {
        substitute module("com.github.Troy1010:tmcommonkotlin-androidx:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-androidx:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-core:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-core:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-coroutines:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-coroutines:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-customviews:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-customviews:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-dsl:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-dsl:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-microphone:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-microphone:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-misc:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-misc:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-rx3:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-rx3:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-tuple:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-tuple:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-view:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-view:$tmcommonkotlin_version")
        substitute module("com.github.Troy1010:tmcommonkotlin-testoverrides:$tmcommonkotlin_version") using module("com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-testoverrides:$tmcommonkotlin_version")
    }
}

dependencies {
    // # Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    // ## Kotlin & Reflect
    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // # AndroidX
    // ## Misc
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'
    testImplementation 'androidx.test:core:1.5.0'
    // ## Espresso
    androidTestImplementation "androidx.test:core:1.5.0"
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test:rules:1.4.0'
    // ## Lifecycle
    def lifecycle_version = "2.4.1"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    // ### Lifecycle & Rx
    implementation "androidx.lifecycle:lifecycle-reactivestreams-ktx:$lifecycle_version"
    // ### Lifecycle & Coroutines
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    // ## Fragment, Activity
    implementation "androidx.fragment:fragment-ktx:1.5.6"
    implementation 'androidx.activity:activity-ktx:1.7.0-alpha01'
    // ## Room
    def room_version = "2.5.0-alpha01"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    // ### Room & Kotlin/Coroutines
    implementation "androidx.room:room-ktx:$room_version"
    // ### Room & Rx
    implementation "androidx.room:room-rxjava3:$room_version"
    // ### Room & Guava
    implementation "androidx.room:room-guava:$room_version"
    // ### Room & Testing
    testImplementation "androidx.room:room-testing:$room_version"
    // ## DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0"
    // ## MultiDex
    implementation 'androidx.multidex:multidex:2.0.1'
    // ## Navigation
    def navigation_version = "2.4.2"
    implementation "androidx.navigation:navigation-fragment-ktx:$navigation_version"
    implementation "androidx.navigation:navigation-ui-ktx:$navigation_version"

    // # JUnit
    // ## JUnit 4
    testImplementation("junit:junit:4.13.2")
    // ## JUnit 5
    testImplementation("org.junit.jupiter:junit-jupiter:5.8.2")
    testImplementation("org.junit.jupiter:junit-jupiter-engine:5.8.2")
    testImplementation("org.junit.jupiter:junit-jupiter-api:5.8.2")
    // ## JUnit 5 Parameterized Testing
    testImplementation("org.junit.jupiter:junit-jupiter-params:5.8.2")
    testRuntimeOnly("org.junit.jupiter:junit-jupiter-params:5.8.2")
    // ## JUnit 5 & JUnit 4
    testRuntimeOnly("org.junit.vintage:junit-vintage-engine:5.8.2")
    // ## JUnit & Android
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'

    // # TMCommonKotlin
    if (project.property("useMavenLocal") == "true") {
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-androidx:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-core:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-coroutines:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-customviews:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-rx3:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-misc:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-tuple:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-view:+"
        testImplementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-testoverrides:+"
        implementation "com.tminus1010.tmcommonkotlin:tmcommonkotlin-imagetotext:+"
    } else {
//    implementation 'com.github.Troy1010:TMCommonKotlin:$tmcommonkotlin_version'
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-core:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-rx3:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-misc:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-tuple:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-view:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-coroutines:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-androidx:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-imagetotext:$tmcommonkotlin_version"
        implementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-customviews:$tmcommonkotlin_version"
        testImplementation "com.github.Troy1010.TMCommonKotlin:tmcommonkotlin-testoverrides:$tmcommonkotlin_version"
    }

    // # Hilt
    implementation 'com.google.dagger:hilt-android:2.41'
    kapt 'com.google.dagger:hilt-android-compiler:2.41'
    // ## Hilt & Test in androidTest/
    androidTestImplementation 'com.google.dagger:hilt-android-testing:2.41'
    kaptAndroidTest 'com.google.dagger:hilt-android-compiler:2.41'
    // ## Hilt & ViewModels
    kapt 'androidx.hilt:hilt-compiler:1.0.0'

    // # Material
    implementation "com.google.android.material:material:1.5.0"

    // # Rx
    implementation 'io.reactivex.rxjava3:rxjava:3.0.7'
    implementation 'io.reactivex.rxjava3:rxandroid:3.0.0'
    // ## Rx & Kotlin
    implementation 'io.reactivex.rxjava3:rxkotlin:3.0.1'
    // ## Rx & Lifecycle
    implementation 'com.trello.rxlifecycle4:rxlifecycle-android-lifecycle-kotlin:4.0.0'

    // # Coroutines
    // ## Coroutines & Rx
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-rx3:1.6.4"
    // ## Coroutines & Test
    testImplementation "org.jetbrains.kotlinx:kotlinx-coroutines-test:1.6.4"

    // # Mockk
    testImplementation "io.mockk:mockk:1.12.7"
    androidTestImplementation "io.mockk:mockk-android:1.12.7"

    // # Moshi
    implementation "com.squareup.moshi:moshi-kotlin:1.13.0"

    // # MPAndroidChart
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // # Dots Indicator
    implementation 'com.github.mazenrashed:DotsIndicatorWithoutViewpager:1.0.0'
    // This is another dots library that looked okay: "com.tbuonomo:dotsindicator:4.3"
}